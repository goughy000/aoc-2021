plugins {
  id 'com.diffplug.spotless' version '6.0.1' apply false
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'com.diffplug.spotless'

  group 'org.example'
  version '1.0-SNAPSHOT'

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  repositories {
    mavenCentral()
  }

  dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
  }

  spotless {
    java {
      googleJavaFormat()
      endWithNewline()
    }
    groovyGradle {
      greclipse()
      indentWithSpaces(2)
      endWithNewline()
    }
  }
}

var verification = project('verification') {
  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.0'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  test {
    useJUnitPlatform()
  }
}

subprojects {

  if (project != verification) {
    verification.dependencies {
      implementation project
    }
  }

  dependencies {
    implementation project(':')
  }
}
